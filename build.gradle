ext.VERSION_MAJOR = 0
ext.VERSION_MINOR = 9
ext.VERSION_PATCH = 3

ext.JDROID_GITHUB_REPOSITORY_OWNER = 'maxirosson'
ext.JDROID_GITHUB_REPOSITORY_NAME = 'jdroid-googleplay-publisher-plugin'

apply plugin: 'com.jdroid.component.builder'
apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'
apply plugin: 'com.gradle.plugin-publish'

description = 'Gradle Plugin to publish android APKs and listings on Google Play'

buildscript {
	repositories {
		mavenCentral()
		jcenter()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
		String localMavenRepo = project.hasProperty('LOCAL_MAVEN_REPO') ? project.ext.get('LOCAL_MAVEN_REPO') : System.getenv('LOCAL_MAVEN_REPO')
		if (localMavenRepo != null) {
			maven {
				url localMavenRepo
			}
		}
	}
	dependencies {
		// https://plugins.gradle.org/plugin/com.gradle.plugin-publish
		classpath "com.gradle.publish:plugin-publish-plugin:0.9.4"

		classpath 'com.jdroidframework:jdroid-internal:0.1.0-SNAPSHOT'
	}
}

repositories {
	mavenCentral()
}

dependencies {
	compile localGroovy()

	compile 'com.jdroidframework:jdroid-java:0.9.5'

	compile 'com.google.apis:google-api-services-androidpublisher:v2-rev30-1.22.0'
	compile 'com.google.api-client:google-api-client:1.19.0'
}

// Configuration to publish your plugin to the plugin portal
pluginBundle {
	website = 'http://www.jdroidframework.com/'
	vcsUrl = 'https://github.com/maxirosson/jdroid-googleplay-publisher'

	plugins {
		GooglePlayPublisherPlugin {
			id = 'com.jdroid.googleplay.publisher'
			displayName = 'Jdroid Google Play Publisher Gradle Plugin'
			description = 'Gradle Plugin to publish android APKs and listings on Google Play'
			tags = ['jdroid', 'android', 'google play']
		}
	}

	mavenCoordinates {
		groupId = "com.jdroidframework"
		artifactId = "jdroid-google-publisher-plugin"
	}
}
